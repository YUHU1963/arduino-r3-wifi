#include <ESP8266WiFi.h>
#include <ESP8266HTTPClient.h>
#include <WiFiUdp.h>
#include <NTPClient.h>


const char* ssid = "iPhone";
const char* password = "12341234";
const char* firebaseHost = "https://smartdoorlock-f8694-default-rtdb.firebaseio.com";

WiFiClientSecure client;           //  use HTTPS WiFi client
WiFiUDP ntpUDP;
NTPClient timeClient(ntpUDP, "pool.ntp.org", -5 * 3600, 60000); // est time setting


void uploadTimeToFirebase(String formattedTime, String date) {
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;

    // Firebase datapath
    String url = String(firebaseHost) + "/timeData/currentTime.json";
    Serial.println("path: " + url);

    client.setInsecure(); // ignore ssl
    http.begin(client, url); // use https
    http.addHeader("Content-Type", "application/json"); 

    
    String jsonData = "{\"easternTime\": \"" + formattedTime +
                      "\", \"date\": \"" + date + "\"}";
    Serial.println("uploaded JSON data: " + jsonData);

    
    int httpResponseCode = http.PUT(jsonData);

    if (httpResponseCode > 0) {
      Serial.print("HTTP code: ");
      Serial.println(httpResponseCode);
      Serial.println("server no response: " + http.getString());
    } else {
      Serial.print("HTTPerror: ");
      Serial.println(httpResponseCode);
    }

    http.end(); 
  } else {
    Serial.println("wifi is not connected cannot upload to Firebase");
  }
}

void setup() {
  Serial.begin(115200);


  WiFi.begin(ssid, password);
  Serial.print("connecting to wifi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }
  Serial.println("\nWiFi connected");

  // initialize  ntp
  timeClient.begin();
}

void loop() {
  static unsigned long lastUpdate = 0;
  if (millis() - lastUpdate >= 2000) { // update every 2 sec
    lastUpdate = millis();

    // update NTP time
    timeClient.update();
    if (timeClient.getEpochTime() == 0) {
      Serial.println("NTP failedï¼");
      return;
    }

    String formattedTime = timeClient.getFormattedTime();
    unsigned long rawTime = timeClient.getEpochTime();

    // initialize date
    unsigned long days = rawTime / 86400L; // total date
    int year = 1970;
    while (days >= 365 + (year % 4 == 0 && (year % 100 != 0 || year % 400 == 0))) {
      days -= 365 + (year % 4 == 0 && (year % 100 != 0 || year % 400 == 0));
      year++;
    }
    int month = 1;
    int dayOfMonth = days;
    const int daysInMonth[] = {31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31};
    for (int i = 0; i < 12; i++) {
      int dim = daysInMonth[i];
      if (i == 1 && (year % 4 == 0 && (year % 100 != 0 || year % 400 == 0))) dim++; 
      if (dayOfMonth < dim) {
        month = i + 1;
        dayOfMonth += 1;
        break;
      }
      dayOfMonth -= dim;
    }
    String date = String(year) + "-" + (month < 10 ? "0" : "") + String(month) + "-" + (dayOfMonth < 10 ? "0" : "") + String(dayOfMonth);

    Serial.println("current time: " + formattedTime);
    Serial.println("current date: " + date);

    // upload to Firebase
    uploadTimeToFirebase(formattedTime, date);
  }
}
